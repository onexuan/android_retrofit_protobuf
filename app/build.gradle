apply plugin: 'com.android.application'

boolean useGoogleProtobuf = rootProject.ext.useGoogleProtobuf

if (useGoogleProtobuf) {
    apply plugin: 'com.google.protobuf'
} else {
    apply plugin: 'com.squareup.wire'
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        applicationId "com.demo.simpleprotobuf"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField "boolean", "USE_GOOGLE_PROTOBUF", useGoogleProtobuf.toString()
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

if (useGoogleProtobuf) {
    protobuf {
        protoc {
            artifact = 'com.google.protobuf:protoc:3.1.0'
        }
        generateProtoTasks {
            all().each { task ->
                task.builtins {
                    remove java
                }
                task.builtins {
                    java {}
                }
            }
        }
    }
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.android.support:appcompat-v7:26.1.0'

    // use google protobuf & retrofit converter
    implementation 'com.google.protobuf:protobuf-java:3.1.0'
    implementation 'com.squareup.retrofit2:converter-protobuf:2.3.0'
    // user wire protobuf & wire converter
    implementation 'com.squareup.retrofit2:converter-wire:2.1.0'
    implementation 'com.squareup.wire:wire-runtime:2.2.0'

    // okhttp
    implementation 'com.squareup.okhttp3:okhttp:3.8.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.4.1'

    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'

    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'

}
